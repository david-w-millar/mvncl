import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.internal.os.OperatingSystem

apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'application'
apply plugin: 'signing'
apply plugin: 'maven'

defaultTasks 'check', 'distTar'

archivesBaseName = name

ext {
  isReleaseVersion = ! version.endsWith('SNAPSHOT')
}

configurations {
  asciidoclet
}

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
  compile "org.codehaus.groovy.modules.http-builder:http-builder:${httpbuilderVersion}"
  compile "org.millarts:groovy-pluralize-en:0.2.1"
  compile "io.airlift:airline:${airlineVersion}"

  compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
  compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
  compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

  asciidoclet "org.asciidoctor:asciidoclet:${asciidocletVersion}"

  testCompile "co.freeside:betamax:${betamaxVersion}"
  testCompile "org.spockframework:spock-core:${spockVersion}"
}

codenarc {
  configFile = file("${project.projectDir}/config/codenarc.groovy")
  ignoreFailures = true
  reportFormat = 'html'
  toolVersion = codenarcVersion
  maxPriority1Violations = 0
  maxPriority2Violations = 0
  maxPriority3Violations = 0
}

processResources {
  filter ReplaceTokens, tokens: [
    version     : version,
    buildDate   : new Date().format("yyyy-MM-dd HH:mm:ss.SSSZ"),
    username    : System.properties["user.name"],
    javaVersion : System.properties["java.version"],
    osName      : System.properties["os.name"],
    osVersion   : System.properties["os.version"]
  ]
}


// ----- [ Packaging and Publishing ] -----

tasks.withType(Groovydoc) {
  link 'http://download.oracle.com/javase/6/docs/api/',
         'java.util',
         'java.lang'

  docTitle    = 'Groovy API Documentation for mvncl $version'
  windowTitle = 'Groovy API for mvncl $version'
  footer      = "<p style='text-align: center'>Groovy API Documentation for mvncl $version - Licensed under the Apache License, Version 2.0 - <a href='https://github.com/david-w-millar/mvncl/'>mvncl</a></p>"
}

task sourceJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
  classifier = 'javadoc'
  from groovydoc
}

javadoc {
  options.docletpath = configurations.asciidoclet.files.asType(List)
  options.doclet = 'org.asciidoctor.Asciidoclet'
}

signing {
  required { isReleaseVersion && gradle.taskGraph.hasTask('uploadArchives') }
  sign configurations.archives
}

artifacts {
  archives javadocJar
  archives sourceJar
  archives jar
}

uploadArchives { task ->
  task.onlyIf { isReleaseVersion || ! project.hasProperty('ci') }

  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(task)) {
          repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
          }
          snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
          }
        }
      }

      pom.project {
        name "${project.name}:${project.version}"
        description "${project.description}"
        inceptionYear '2014'
        url project_url
        packaging 'jar'

        scm {
          connection 'git@github.com:david-w-millar/mvncl.git'
          developerConnection 'git@github.com:david-w-millar/mvncl.git'
          url project_url
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'david-w-millar'
            name 'David W Millar'
            email 'david.w.millar@gmail.com'
            organization project_url
            organizationUrl project_url
          }
        }

      }
    }
  }
}

